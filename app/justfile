# Load .env file
set dotenv-load := true


######## Local Environment commands ########
lockdepsprod:
    pip-compile -o requirements/main.txt pyproject.toml

lockdeps:
    pip-compile -o requirements/dev.txt pyproject.toml --extra=dev


install:
    pip-sync requirements/dev.txt requirements/main.txt


sync: lockdeps install

######## General commands ########
start: check_secrets_exist
    python -m twisted

check_secrets_exist:
    #!/usr/bin/env python3
    import os
    envs = [
        'SLACK_BOT_TOKEN', 
        'SLACK_SIGNING_SECRET', 
        'SLACK_APP_TOKEN', 
        'TWIST_OAUTH_TOKEN',
        'GITHUB_TOKEN',
        'AWS_AMAZON_KEY',
        'AWS_AMAZON_SECRET',
        'AWS_PROFILE',
        'AWS_ACCOUNT',
    ]
    for env in envs:
        assert os.getenv(env), f"Missing required {env!r} environment variable"


######## Docker commands ########
build:
    docker build . -t twisted

enter: build
    docker run -it --rm --env-file .env -p 3000:3000 -v $PWD/twisted:/app/twisted -v $HOME/.aws:/root/.aws twisted /bin/bash

run: build check_secrets_exist
    docker run -it --rm --env-file .env -p 3000:3000 -v $PWD/twisted:/app/twisted -v $HOME/.aws:/root/.aws twisted

runit: check_secrets_exist
    docker run -it --rm --env-file .env -p 3000:3000 -v $PWD/twisted:/app/twisted -v $HOME/.aws:/root/.aws twisted

# Update image used for deploys
updateimage:
    docker build . -t ambro17/twisted:production --platform=linux/amd64
    docker push ambro17/twisted:production
